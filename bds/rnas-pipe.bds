#! /usr/bin/env bds
#----------------------------------------------------------------------------------------------------
#
#  Map Reads to the reference genome
#                                                                                
#----------------------------------------------------------------------------------------------------

#------------------------------
#  Serine #MonashFlavour
#------------------------------
#
#----------------------------------------------------------------------------------------------------
#
#  About STARmergeNalign.bds
#
#   -  run -h, -help, --help to get more help of the command line arguments
#   -  takes a whole directory that contatins fastq files and maps to the reference genome
#   -  identifies samples that span different lanes and merges then together during STAR run,
#      outputting sigle bam file per sample.
#   -  auto matches paired end
#   -  creates strandedNo and strandedReverse directories under the current directory and 
#      performce read count using htseq-count outputting results in corresponding directories.

#----------------------------------------------------------------------------------------------------
#
#  Usage:
#
#  1. STAR outputs all file into the current directory i.e the directory STAR was executed from.
#  2. The best practise is to make new directory for your STARmergeNaling.bds run and execute
#     STARmergeNalign.bds script within that directory.
#  
#----------------------------------------------------------------------------------------------------
#  Input Parametrs
#----------------------------------------------------------------------------------------------------

string check4input 
string star           help select if you would like to run star. Options: yes or no
string fastqDir       help path to the directory with fastq file       
string genomeIndex    help path to the directory with genome index for the coresponding species
string count          help Select if you would like to count reads. Options: yes or no
string gtfFile        help path to the directory with gtf file
string prePro         help Select if you would like to preprocess your bam files for RNA-SeQC run Options: yes or no
string fastaRef       help path to the reference fasta file, required when doing preprocessing for RNA-SeQC run

#----------------------------------------------------------------------------------------------------
#
# Main
#
#----------------------------------------------------------------------------------------------------

#------------------------------
# Sanity check
#------------------------------

#----------------------------------------------------------------------------------------------------

if( !star.isEmpty() ) {

    check4input = "populated"   
    if( ( star != "yes" ) && ( star != "no" ) ) error "Usage: -star yes or no"
    if( fastqDir.isEmpty() )  error "Usage: -fastqDir /path/to/fastq_reads/"
    if( genomeIndex.isEmpty() )  error "Usage: -genomeIndex /path/to/genome_index/"
}

#----------------------------------------------------------------------------------------------------

if( !count.isEmpty() ) {

    check4input = "populated"   
    if( ( count != "yes" ) && ( count != "no" ) ) error "Usage: -count yes or no"
    if( gtfFile.isEmpty() )  error "Usage: -gtfFile /path/to/gtf_file/"
}

#----------------------------------------------------------------------------------------------------

if( !prePro.isEmpty() ) {

    check4input = "populated"   
    if( ( prePro != "yes" ) && ( prePro != "no" ) ) error "Usage: -prePro yes or no"
    if( fastaRef.isEmpty() ) error "Usage: -fastaRef /path/to/reference_fasta_file"
}

#----------------------------------------------------------------------------------------------------

if( check4input.isEmpty() ) sys ~/scripts/bds/rnas-pipe.bds --help

#----------------------------------------------------------------------------------------------------

#------------------------------
# Map reads
#------------------------------

# a list with files names, that is sorted uniquely
string{} uniqueNamesMap 

#------------------------------
# Get list of unique names
#------------------------------

for (string fq : fastqDir.dirPath() ) {
     # make sure we have fastq files only
     if ( fq.endsWith(".fastq.gz") ) {

         string getR1Index = fq.lastIndexOf("R1") 
         
         if ( getR1Index != -1 ) {
             # get the root of the file name with regex
             string[] tempRoot = fq.split("_L[0-9]{3}_R[0-9].fastq.gz$")
             string rootName = tempRoot[0]

             if ( !uniqueNamesMap.hasKey(rootName) ) {
                 # append to the list if it isn't in the list already
                 uniqueNamesMap{rootName} = ''
             }
             uniqueNamesMap{rootName} += ","+fq
         }
     }       
}

#------------------------------
# Execution of STAR
#------------------------------

#------------------------------
# Sanity check if pwd hasn't
# got any bam files in it
#------------------------------

if ( star == "yes" ) {
    #loop over each key in the map
    for (string fqread : uniqueNamesMap.keys() ) {
        #---------------------------------------------
        # define banch of empty variables for later use
        string[] listR1
        string[] listR2
        string[] laneList

        string read1 
        string read2
        string lanes

        string preFix
        #---------------------------------------------
        
        # get value for each key 
        string mapValue = uniqueNamesMap{fqread} 
        # break the values into list
        # side effect of BDS
        string[] getList = mapValue.split(",")
        # this is length of the list
        # -1, because there is an extra comma in the
        # value list. side effect of BDS
	int getListSize = getList.size()-1

        # loop over each item in the getList
        for ( int i : range(1,getListSize) ) {
            # get fastq reads and get it pair 
            string singleRead = getList[i]
            string pairedEnd = singleRead.swapExt("R1.fastq.gz", "R2.fastq.gz")
            # exctract lanes information from the file name
            string[] tmpLane = singleRead.split("_")
            string tmpLane2 = tmpLane.pop()
            string getLane = tmpLane.pop()

            listR1.add(singleRead)
            listR2.add(pairedEnd)
            laneList.add(getLane)

            # concatenate for STAR usage 
            read1 = listR1.join(",")
            read2 = listR2.join(",")
            lanes = laneList.join(",")
        }
        # get file name pre fix for STAR naming 
        if ( preFix.isEmpty() ) preFix = fqread+"_"
        #print("$read1 $read2 $preFix\n")
        task STAR --runThreadN 10 \
                   --genomeDir $genomeIndex \
                   --outSAMtype BAM Unsorted \
                   --outSAMattrRGline ID:$lanes CN:AGRF DS:RNA-seq PL:ILLUMINA PM:MiSeq SM:$fqread \
                   --outSAMunmapped Within \
                   --readFilesCommand zcat \
                   --readFilesIn $read1 $read2 \
                   --outFileNamePrefix $preFix
    }
    wait
}

# gets the current directory to with bam files
string workingDir = "$PWD"

#------------------------------
# piacrd secssion
# bam file preprocessing 
# for RNA-SeQC run
#------------------------------

if ( prePro == "yes" ) {

    string preqcBamFiles = "preqcBamFiles"

    if ( !preqcBamFiles.exists() ) {
        preqcBamFiles.mkdir()
    }

    # loop over the directory and submit all bam files for sorting
    for (string bamFile : workingDir.dirPath() ) {
        if( bamFile.endsWith(".bam") ) {

            string[] temp = bamFile.split("/")
            string name = temp.pop()
            string[] temp2 = name.split('_Aligned')
            string rootName = temp2[0]
            string sortedName = preqcBamFiles+"/"+rootName+"_sorted.bam"

            task picard SortSam TMP_DIR=tmp/ \
                                INPUT=$bamFile \
                                OUTPUT=$sortedName \
                                SORT_ORDER=coordinate
        }
    }
    
    wait

    string preqcDir = "$PWD"+"/"+preqcBamFiles

    # waint until all bam files in the directory were sorted
    # then reorder all sorted bam files

    for (string bamFile : preqcDir.dirPath() ) {

        if( bamFile.endsWith("sorted.bam") ) {

            string[] temp = bamFile.split("/")
            string name = temp.pop()
            string[] temp2 = name.split('_sorted')
            string rootName = temp2[0]
            string sortedName = preqcBamFiles+"/"+rootName+"_sorted_reordered.bam"

            task picard ReorderSam TMP_DIR=tmp/ \
                                   INPUT=$bamFile \
                                   OUTPUT=$sortedName \
                                   REFERENCE=$fastaRef

        }
    }

    wait 

    # wait until all bam fiels in the direcotory were reordered
    # then mark duplicates

    for (string bamFile : preqcDir.dirPath() ) {
    
        if( bamFile.endsWith("reordered.bam") ) {
    
            string[] temp = bamFile.split("/")
            string name = temp.pop()
            string[] temp2 = name.split('_sorted')
            string rootName = temp2[0]
            string sortedName = preqcBamFiles+"/"+rootName+"_sorted_reordered_marked_dups.bam"
            string metricsName = preqcBamFiles+"/"+rootName+"_sorted_reordered_marked_dups.metrics"

            task picard MarkDuplicates TMP_DIR=tmp/ \
                                       CREATE_INDEX=true \
                                       INPUT=$bamFile \
                                       OUTPUT=$sortedName \
                                       METRICS_FILE=$metricsName

        }
    }

    wait
}

#------------------------------
# htseq-count 
#------------------------------

if ( count == "yes" ) {
    
    string strandedReverse = "strandedReverse"
    string strandedNo = "strandedNo"

    if( (!strandedReverse.exists()) && (!strandedNo.exists()) ) {
        strandedReverse.mkdir()
        strandedNo.mkdir()
    }
    
    for (string bamFile : workingDir.dirPath() ) {
    
        if( bamFile.endsWith(".bam") ) {
    
            string[] temp = bamFile.split("/")
            string name = temp.pop()
            string[] temp2 = name.split('_Aligned')
            string rootName = temp2[0]
    
            string runOne = strandedReverse+"/"+rootName+".txt" 
            string runTwo = strandedNo+"/"+rootName+".txt" 

            task htseq-count --quiet \
                             --stranded=reverse \
                             --format=bam \
                             --order=pos \
                             $bamFile $gtfFile > $runOne

            task htseq-count --quiet \
                             --stranded=no \
                             --format=bam \
                             --order=pos \
                             $bamFile $gtfFile > $runOne



        }
    }
}

#------------------------------
# End
#------------------------------
